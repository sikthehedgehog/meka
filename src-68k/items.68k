;****************************************************************************
; ITYPE_*
; Possible item types
;****************************************************************************

                    rsreset
                    rs.b 1      ; No item
ITYPE_SHIELD:       rs.b 1      ; Shield
ITYPE_BOMB:         rs.b 1      ; Bomb

;****************************************************************************
; InitItem
; Initializes the item spawner.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

InitItem:
    clr.b   (ItemType)                  ; No item yet
    move.w  #600, (ItemTimer)           ; Spawn first item 10 seconds in
    rts                                 ; End of subroutine

;****************************************************************************
; UpdateItem
; Updates the item's movement and such.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

UpdateItem:
    subq.w  #1, (ItemTimer)             ; Spawn a new item?
    bne.s   @Spawn
    move.w  #900, (ItemTimer)
    move.w  #-32, (ItemX)
    jsr     (Rand).w
    and.b   #$01, d7
    addq.b  #1, d7
    move.b  d7, (ItemType)
@Spawn:
    
    move.w  (ItemX), d0                 ; Move item forwards
    addq.w  #1, d0
    move.w  d0, (ItemX)
    
    add.w   d0, d0                      ; Make item swing around
    and.w   #$FF, d0
    add.w   d0, d0
    lea     (Sines), a6
    move.w  (a6,d0.w), d0
    asr.w   #2, d0
    add.w   #$70, d0
    move.w  d0, (ItemY)
    
    rts                                 ; End of subroutine

;****************************************************************************
; DrawItem
; Draws the item on screen.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

DrawItem:
    tst.b   (ItemType)                  ; Is there an item?
    beq.s   @NoItem
    
    move.w  (ItemX), d0                 ; Get coordinates on screen
    move.w  (ItemY), d1
    sub.w   #12, d0
    sub.w   #12, d1
    
    moveq   #0, d2                      ; Get item sprite
    move.b  (ItemType), d2
    subq.b  #1, d2
    move.w  d2, d7
    lsl.w   #3, d2
    add.w   d7, d2
    add.w   #$4000+VramItems, d2
    
    moveq   #%1010, d3                  ; Draw item
    jsr     (AddSprite).w
    
@NoItem:
    rts                                 ; End of subroutine
